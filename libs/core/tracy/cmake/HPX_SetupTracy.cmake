# Copyright (c) 2026 Hartmut Kaiser
#
# SPDX-License-Identifier: BSL-1.0
# Distributed under the Boost Software License, Version 1.0. (See accompanying
# file LICENSE_1_0.txt or copy at http://www.boost.org/LICENSE_1_0.txt)

include(HPX_AddDefinitions)

# compatibility with older CMake versions
if(TRACY_ROOT AND NOT Tracy_ROOT)
  set(Tracy_ROOT
      ${TRACY_ROOT}
      CACHE PATH "Tracy base directory"
  )
  unset(TRACY_ROOT CACHE)
endif()

if(NOT HPX_WITH_FETCH_TRACY)
  find_package(Tracy)
  if(NOT Tracy_FOUND)
    hpx_error(
      "Could not find Tracy. Set Tracy_ROOT as a CMake or environment variable to point to the Tracy root install directory. Alternatively, set HPX_WITH_FETCH_TRACY=ON to fetch Tracy using CMake's FetchContent (when using this option Asio will be installed together with HPX, be careful about conflicts with separately installed versions of Tracy)."
    )
  endif()
  if(TARGET Tracy::TracyClient AND NOT TARGET tracy::tracy)
    add_library(tracy::tracy ALIAS Tracy::TracyClient)
  endif()
  # We cannot detect whether the system Tracy was built with
  # TRACY_DBGHELP_LOCK=HpxDbgHelp (Tracy does not record it on the imported
  # target). Warn on Windows so a mismatch is not silent; either rebuild the
  # system Tracy with the define, or set HPX_WITH_FETCH_TRACY=ON.
  if(WIN32)
    hpx_warn(
      "HPX_WITH_FETCH_TRACY=OFF on Windows: cannot verify the system Tracy was built with TRACY_DBGHELP_LOCK=HpxDbgHelp. Rebuild Tracy with -DTRACY_DBGHELP_LOCK=HpxDbgHelp, or set HPX_WITH_FETCH_TRACY=ON."
    )
  endif()
elseif(NOT TARGET tracy::tracy)
  if(FETCHCONTENT_SOURCE_DIR_TRACY)
    hpx_info(
      "HPX_WITH_FETCH_TRACY=${HPX_WITH_FETCH_TRACY}, Tracy will be used through CMake's FetchContent and installed alongside HPX (FETCHCONTENT_SOURCE_DIR_TRACY=${FETCHCONTENT_SOURCE_DIR_TRACY})"
    )
  else()
    hpx_info(
      "HPX_WITH_FETCH_TRACY=${HPX_WITH_FETCH_TRACY}, TRACY will be fetched using CMake's FetchContent and installed alongside HPX (HPX_WITH_TRACY_TAG=${HPX_WITH_TRACY_TAG})"
    )
  endif()

  include(FetchContent)
  fetchcontent_declare(
    tracy
    GIT_REPOSITORY https://github.com/wolfpld/tracy
    GIT_TAG ${HPX_WITH_TRACY_TAG}
    GIT_SHALLOW TRUE
  )

  # Set the correct build options for Tracy and make it available. 0.14 defaults
  # TRACY_ENABLE OFF; without forcing it on, a HPX build with HPX_WITH_TRACY=ON
  # compiles, links, and records nothing.
  set(TRACY_ENABLE
      ON
      CACHE BOOL "" FORCE
  )
  set(TRACY_FIBERS
      ON
      CACHE BOOL "" FORCE
  )
  set(TRACY_ON_DEMAND
      ON
      CACHE BOOL "" FORCE
  )

  fetchcontent_makeavailable(tracy)

  set(TRACY_ROOT ${tracy_SOURCE_DIR})

  # adjust more settings for the TracyClient target
  target_compile_definitions(
    TracyClient
    PUBLIC $<$<OR:$<CONFIG:Release>,$<CONFIG:RelWithDebInfo>>:TRACY_NO_VERIFY>
  )
  target_compile_definitions(
    TracyClient PUBLIC $<$<CONFIG:Debug>:TRACY_VERBOSE>
  )
  # Serialise Tracy's DbgHelp calls against HPX's own via the wrappers in
  # hpx_debugging (dbghelp_lock.cpp). Only applies on the FetchContent path; a
  # system-supplied Tracy must be built with the same define for full interlock
  # (documented in optimizing_hpx_applications.rst).
  #
  # BUILD_SHARED_LIBS=ON turns TracyClient into a shared library, which would
  # then need HpxDbgHelp* resolved at its own link step. hpx_debugging provides
  # those symbols but TracyClient does not depend on it; hpx_tracy is what links
  # both, so a static TracyClient resolves when hpx_tracy links, while a shared
  # TracyClient does not. Static TracyClient (the default when BUILD_SHARED_LIBS
  # is unset or OFF) is the supported configuration.
  if(WIN32)
    target_compile_definitions(TracyClient PUBLIC TRACY_DBGHELP_LOCK=HpxDbgHelp)
  endif()
  target_compile_features(TracyClient PRIVATE cxx_std_${HPX_CXX_STANDARD})

  # cmake-format: off
  set_target_properties(
    TracyClient PROPERTIES
        FOLDER "Core/Dependencies"
        POSITION_INDEPENDENT_CODE ON
  )
  # cmake-format: on

  add_library(tracy INTERFACE)
  target_include_directories(
    tracy SYSTEM INTERFACE $<BUILD_INTERFACE:${TRACY_ROOT}/public>
                           $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
  )
  target_link_libraries(tracy INTERFACE TracyClient)

  install(
    TARGETS tracy
    EXPORT HPXTracyTarget
    COMPONENT core
  )

  install(
    DIRECTORY ${TRACY_ROOT}/public/
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
    COMPONENT core
    FILES_MATCHING
    PATTERN "*.hpp"
  )

  export(
    TARGETS tracy
    NAMESPACE tracy::
    FILE "${CMAKE_BINARY_DIR}/lib/cmake/${HPX_PACKAGE_NAME}/HPXTracyTarget.cmake"
  )

  install(
    EXPORT HPXTracyTarget
    NAMESPACE tracy::
    FILE HPXTracyTarget.cmake
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/${HPX_PACKAGE_NAME}
    COMPONENT cmake
  )

  add_library(tracy::tracy ALIAS tracy)
endif()
